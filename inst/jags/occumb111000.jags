model {
    # Sequence read counts
    for (j in 1:J) {
        for (k in 1:K) {
            y[1:I, j, k] ~ dmulti(pi[1:I, j, k], N[j, k])
        }
    }

    # Cell probabilities for sequence reads
    for (j in 1:J) {
        for (k in 1:K) {
            for (i in 1:I) {
                pi[i, j, k] <- u[i, j, k] * x[i, j, k] / sum_ux[j, k]
                x[i, j, k] ~ dgamma(phi[i], 1)
            }
            sum_ux[j, k] <- ifelse(sum(u[1:I, j, k]) > 0,
                                   sum(u[1:I, j, k] * x[1:I, j, k]),
                                   1)
        }
    }

    # Availability of species sequence
    for (i in 1:I) {
        for (j in 1:J) {
            for (k in 1:K) {
                u[i, j, k] ~ dbern(z[i, j] * theta[i])
            }
        }
    }

    # Species site occupancy
    for (i in 1:I) {
        for (j in 1:J) {
            z[i, j] ~ dbern(psi[i])
        }
    }

    # Species-level parameters
    for (i in 1:I) {
        log(phi[i])     <- inprod(alpha[i, ], cov_phi[])
        logit(theta[i]) <- inprod(beta[i, ], cov_theta[])
        logit(psi[i])   <- inprod(gamma[i, ], cov_psi[])

        for (m in 1:length(m_phi)) {
            alpha[i, m] <- spec_eff[i, m_phi[m]]
        }
        for (m in 1:length(m_theta)) {
            beta[i, m]  <- spec_eff[i, m_theta[m]]
        }
        for (m in 1:length(m_psi)) {
            gamma[i, m] <- spec_eff[i, m_psi[m]]
        }

        spec_eff[i, 1:M] ~ dmnorm.vcov(Mu[1:M], Sigma)
    }

    # Priors
    for (m in 1:M) {
        Mu[m] ~ dnorm(0, prior_prec)
    }

    for (m in 1:M) {
        Sigma[m, m] <- pow(sigma[m], 2)
    }
    for (m1 in 1:(M - 1)) {
        for (m2 in (m1 + 1):M) {
            Sigma[m1, m2] <- rho[m1, m2] * sigma[m1] * sigma[m2]
            Sigma[m2, m1] <- rho[m1, m2] * sigma[m1] * sigma[m2]
        }
    }

    for (m in 1:M) {
        sigma[m] ~ dunif(0, prior_ulim)
    }
    for (m1 in 1:M) {
        for (m2 in 1:M) {
            rho[m1, m2] ~ dunif(-1, 1)
        }
    }
}

